
// #pragma
#pragma kernel InitialState Simulate=InitialState INITIAL_STATE
#pragma kernel Simulate     Simulate=Simulate     SIMULATE

#define TILE_SIZE 4

#include "Packages/com.unity.render-pipelines.core/ShaderLibrary/Common.hlsl"
#include "Packages/com.unity.render-pipelines.high-definition/Runtime/ShaderLibrary/ShaderVariables.hlsl"

CBUFFER_START(_FluidSimVolume)
float3 _FluidSimVolumeRes;
float _VectorFieldSpeed;
CBUFFER_END

#ifdef INITIAL_STATE
Texture3D<float4> _InitialStateVolumeTexture;
#elif SIMULATE
Texture3D<float4> _VectorField;
#endif

RWTexture3D<float4> _SimulationBuffer0;
RWTexture3D<float4> _SimulationBuffer1;

[numthreads(TILE_SIZE, TILE_SIZE, TILE_SIZE)]
void Simulate(uint3 id : SV_DispatchThreadID)
{
#ifdef INITIAL_STATE
    _SimulationBuffer0[id.xyz] = float4((float3)id.xyz, 0.0);
    _SimulationBuffer1[id.xyz] = _InitialStateVolumeTexture[id.xyz];
#elif SIMULATE

    float3 pos = _SimulationBuffer0[id.xyz].xyz;
    float3 vfUVW = (pos / _FluidSimVolumeRes);

    float3 vf = SAMPLE_TEXTURE3D_LOD(_VectorField, s_linear_clamp_sampler, vfUVW, 0).rgb;
    vf *= (unity_DeltaTime * _VectorFieldSpeed);
    //float3 vf = (pos / _FluidSimVolumeRes) * 2.0 - 1.0;
    //vf = float3(vf.y, -vf.x, 0.0) / vf.z;

    float3 outputPos = pos + vf;

    if (any(outputPos < 0.0) || any(outputPos >= _FluidSimVolumeRes))
        outputPos = 0.5 * _FluidSimVolumeRes;

    _SimulationBuffer0[id.xyz] = float4(outputPos, 0.0);
#endif
}
